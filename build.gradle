group 'io.gbaldeck'
version '0.0.1'

buildscript {
    ext.kotlin_version = '1.1.61'
    ext.output_filename = "kwc"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
}

sourceSets {
    main.kotlin.srcDirs += 'src/kotlin'

    if(project.gradle.startParameter.taskNames.contains("buildSample")) {
        main.kotlin.srcDirs += 'sample/kotlin'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "${projectDir}/build/js/${output_filename}.js"
//    kotlinOptions.sourceMap = true
//    kotlinOptions.moduleKind = 'commonjs'
}

task buildSample {
    dependsOn 'build'
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/web"

    dependsOn classes
}

assemble.dependsOn assembleWeb